let lines = fs::read("day-02.input").trim().split("\n");
#let lines = ```
#7 6 4 2 1
#1 2 7 8 9
#9 7 6 2 1
#1 3 2 4 5
#8 6 4 4 1
#1 3 6 7 9
#```.trim().split("\n");

let reports = [];
for line in lines {
    let report = [];
    for level in line.split(" ") {
        report.push(number(level));
    }
    reports.push(report);
}

let check = function(report) {
    if report != report.sorted() and report != report.sorted().reversed() {
        return false;
    }

    for i in report.count() - 1 {
        let difference = math::abs(report[i] - report[i + 1]);
        if difference < 1 or difference > 3 {
            return false;
        }
    }

    return true;
};

let part1 = 0;
for report in reports {
    if check(report) {
        part1 = part1 + 1;
    }
}
println("PART 1: " + repr(part1));

let part2 = 0;
for report in reports {
    for i in report.count() {
        let adjusted = report.slice(0, i) + report.slice(i + 1, report.count());
        if check(adjusted) {
            part2 = part2 + 1;
            break;
        }
    }
}
println("PART 2: " + repr(part2));
################################################################################
# PART 1: 585
# PART 2: 626
