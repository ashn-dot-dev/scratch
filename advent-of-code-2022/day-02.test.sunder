import "std";

# Normalized choices (worth their individual point values).
const ROCK     = 1u;
const PAPER    = 2u;
const SCISSORS = 3u;

func normalize(choice: byte) usize {
    if 'A' <= choice and choice <= 'C' {
        return (:usize)choice - 'A' + 1;
    }

    if 'X' <= choice and choice <= 'Z' {
        return (:usize)choice - 'X' + 1;
    }

    std::assert(false); # unreachable
    return 0;
}

func run_round(opnt_choice_normalized: usize, your_choice_normalized: usize) usize {
    std::assert(ROCK <= opnt_choice_normalized and opnt_choice_normalized <= SCISSORS);
    std::assert(ROCK <= your_choice_normalized and your_choice_normalized <= SCISSORS);

    if your_choice_normalized == opnt_choice_normalized {
        return your_choice_normalized + 3; # draw
    }

    if your_choice_normalized == ROCK     and opnt_choice_normalized == SCISSORS
    or your_choice_normalized == PAPER    and opnt_choice_normalized == ROCK
    or your_choice_normalized == SCISSORS and opnt_choice_normalized == PAPER {
        return your_choice_normalized + 6; # win
    }

    return your_choice_normalized; # loss
}

func run_round_part1(opnt_choice: byte, your_choice: byte) usize {
    return run_round(normalize(opnt_choice), normalize(your_choice));
}

func run_round_part2(opnt_choice: byte, outcome: byte) usize {
    var opnt_choice_normalized = normalize(opnt_choice);

    const LOSE: byte = 'X';
    const DRAW: byte = 'Y';
    const WIN: byte = 'Z';

    if outcome == LOSE and opnt_choice_normalized == ROCK {
        return run_round(opnt_choice_normalized, SCISSORS);
    }
    if outcome == LOSE and opnt_choice_normalized == PAPER {
        return run_round(opnt_choice_normalized, ROCK);
    }
    if outcome == LOSE and opnt_choice_normalized == SCISSORS {
        return run_round(opnt_choice_normalized, PAPER);
    }

    if outcome == WIN and opnt_choice_normalized == ROCK {
        return run_round(opnt_choice_normalized, PAPER);
    }
    if outcome == WIN and opnt_choice_normalized == PAPER {
        return run_round(opnt_choice_normalized, SCISSORS);
    }
    if outcome == WIN and opnt_choice_normalized == SCISSORS {
        return run_round(opnt_choice_normalized, ROCK);
    }

    std::assert(outcome == DRAW);
    return run_round(opnt_choice_normalized, opnt_choice_normalized);
}

func main() void {
    var result = std::file::open("day-02.input", std::file::OPEN_READ);
    var file = result.value();
    defer file.close();

    var reader = std::reader::init[[typeof(file)]](&file);

    var part1_total_score = 0u;
    var part2_total_score = 0u;
    for true {
        var result = std::read_line(reader);
        var optional = result.value();
        if optional.is_empty() {
            break;
        }

        var line = optional.value();
        defer std::slice[[byte]]::delete(line);

        std::assert(countof(line) != 0);
        part1_total_score = part1_total_score + run_round_part1(line[0], line[2]);
        part2_total_score = part2_total_score + run_round_part2(line[0], line[2]);
    }

    std::print_format_line(
        std::out(),
        "PART 1: {}",
        (:[]std::formatter)[std::formatter::init[[usize]](&part1_total_score)]);

    std::print_format_line(
        std::out(),
        "PART 2: {}",
        (:[]std::formatter)[std::formatter::init[[usize]](&part2_total_score)]);
}
################################################################################
# PART 1: 13268
# PART 2: 15508
